plugins {
    id("java")
    id("com.github.johnrengelman.shadow").version("7.1.2")
    id("net.kyori.blossom").version("1.3.1")
}

allprojects {
    apply(plugin: "java")
    apply(plugin: "com.github.johnrengelman.shadow")
    apply(plugin: "net.kyori.blossom")

    repositories {
        mavenCentral()
        maven {
            name("JitPack Maven")
            url("https://jitpack.io")
        }
        maven {
            name("Exceptionflug Maven")
            url("https://mvn.exceptionflug.de/repository/exceptionflug-public")
        }
        maven {
            name("PaperMC Maven")
            url("https://repo.papermc.io/repository/maven-public")
        }
    }
    dependencies {
        annotationProcessor("org.projectlombok:lombok:${project.lombok_version}")
        compileOnly("org.projectlombok:lombok:${project.lombok_version}")
        compileOnly("dev.simplix:protocolize-api:${project.protocolize_version}")
        implementation("com.github.simplix-softworks:simplixstorage:${project.simplixstorage_version}")
    }

    group = project.maven_group
    def ENV = System.getenv()
    String realVersion = "${project.plugin_version}"

    realVersion += "." + (ENV.BUILD_NUMBER ? "${ENV.BUILD_NUMBER}" : Integer.MAX_VALUE)
    realVersion += ENV.COMMIT_SHA ? "+${ENV.COMMIT_SHA}" : ""
    switch (ENV.BUILD_TYPE) {
        case "RELEASE":
            realVersion += "-stable"
            break
        case "BETA":
            realVersion += "-beta"
            break
        default:
            realVersion += "-dev"
    }

    version = realVersion
    description = project.plugin_description

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    blossom {
        replaceToken("{plugin_id}", project.plugin_id)
        replaceToken("{plugin_name}", project.plugin_name)
        replaceToken("{plugin_version}", version)
        replaceToken("{plugin_url}", project.plugin_url)
        replaceToken("{plugin_description}", project.plugin_description)
        replaceToken("{plugin_author}", project.plugin_author.replaceAll(", ", "\", \""))
    }

    shadowJar {
        from("LICENSE")

        dependencies {
            exclude(dependency("com.esotericsoftware.yamlbeans:yamlbeans:.*"))
            exclude(dependency("org.jetbrains:annotations:.*"))
            exclude(dependency("org.json:json:.*"))
        }

        relocate("de.leonhard.storage", "${project.maven_group}.${project.plugin_id.replaceAll("-", "")}.lib.de.leonhard.storage")
        // Remove '-all' suffix
        classifier(null)
    }
}

shadowJar {
    // Disable cache
    outputs.upToDateWhen { false }
    dependsOn(project.subprojects.collect {
        // Possible solution, it works.
        it.name == "${project.plugin_id.replaceAll("-", "")}-common" ? it.tasks.jar : it.tasks.shadowJar
    })

    doFirst {
        // Clean up legacy files.
        delete fileTree("build/tmp/merged")
        copy {
            from {
                project.subprojects.collect {
                    "${it.buildDir}/classes/java/main"
                }
            }
            from {
                project.subprojects.collect {
                    "${it.buildDir}/resources/main"
                }
            }
            into("build/tmp/merged")
        }
    }

    from("build/tmp/merged")
}